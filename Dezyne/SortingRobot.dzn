import IIngest.dzn;
import ISensor.dzn;
import ISortingSystem.dzn;

component SortingRobotSystem
{
	provides IMaster master;

	system
	{
		Master m;
		m.master <=> master;
		
		Sensor factorFloorSensor;
		factorFloorSensor.sensor <=> m.factoryFloorSensor;
		
		Ingester i;
		Motor wheelMotor;
		Sensor wheelStopSensor;
		Timer ingestTimer;
		i.ingest <=> m.ingest;
		i.wheelMotor <=> wheelMotor.motor;
		i.wheelStopSensor <=> wheelStopSensor.sensor;
		i.timer <=> ingestTimer.timer;
		
		Sensor beltSensorWhite;
		Sensor beltSensorBlack;
		SortingSystem sortingSystem;
		ColourSensor cs;
		Actuator whiteActuator;
		Actuator blackActuator;
		Motor beltMotor;
		Timer sortingTimer;
		sortingSystem.sortingSystem <=> m.sortingSystem;
		sortingSystem.colourSensor <=> cs.colourSensor;
		sortingSystem.beltSensorWhite <=> beltSensorWhite.sensor;
		sortingSystem.beltSensorBlack <=> beltSensorBlack.sensor;
		sortingSystem.whiteActuator <=> whiteActuator.actuator;
		sortingSystem.blackActuator <=> blackActuator.actuator;
		sortingSystem.beltMotor <=> beltMotor.motor;
		sortingSystem.timer <=> sortingTimer.timer;
	}
}

interface IMaster
{
	in void start();
	in void stop();
	in void emergency();
	in void forceWait();
	in void cancelWait();

	behaviour
	{
		on start: {}
		on stop: {}
		on emergency: {}
		on forceWait: {}
		on cancelWait: {}
	}
}

component Master
{
	provides IMaster master;
	requires IIngest ingest;
	requires ISensor factoryFloorSensor;
	requires ISortingSystem sortingSystem;

	behaviour
	{
		enum State {Off, Idle, Waiting, Error, IngestingDisk, Sorting};
		State state = State.Off;
		bool waitNext = false;
		

		[state.Off]
		{
			on master.start(): {state = State.Idle;}
			on master.stop(): {}
			on master.emergency(): {}
			on master.forceWait(): {}
			on master.cancelWait(): {}
		}
		[state.Idle]
		{
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;}
			on master.forceWait(): {state = State.Waiting;} 
			on master.cancelWait(): {}
			
			on factoryFloorSensor.high(): {
				state = State.IngestingDisk;
				ingest.startIngest();
			}
		}
		[state.Waiting] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;}
			on master.forceWait(): {} 
			on master.cancelWait(): {state = State.Idle;}
		}
		[state.Error] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;}
			on master.forceWait(): {}
			on master.cancelWait(): {} 
		}
		[state.IngestingDisk] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;} 
			on master.forceWait(): {
				waitNext = true;
			}
			on master.cancelWait(): {}
			
			on ingest.finished(): {
				state = State.Sorting;
				sortingSystem.startSorting();
			}
		}
		[state.Sorting] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;} 
			on master.forceWait(): {
			waitNext = true;
			}
			on master.cancelWait(): {}
			
			on sortingSystem.finished(): {
				if (waitNext) {
					waitNext = false;
					state = State.Waiting;
				} else {
					state = State.Idle;
				}
			}
		}
	}
}
