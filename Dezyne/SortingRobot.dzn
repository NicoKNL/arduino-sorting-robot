import IIngest.dzn;
import ISensor.dzn;
import ISortingSystem.dzn;

component SortingRobotSystem
{
	provides IMaster master;

	system
	{
		Master m;
		Ingester i;
		Sensor factorFloorSensor;
		Sensor beltSensorWhite;
		Sensor beltSensorBlack;
		SortingSystem sortingSystem;
		ColourSensor cs;
		Actuator whiteActuator;
		Actuator blackActuator;
		Timer timer;
		
		m.master <=> master;
		i.ingest <=> m.ingest;
		factorFloorSensor.sensor <=> m.factoryFloorSensor;
		
		sortingSystem.sortingSystem <=> m.sortingSystem;
		sortingSystem.colourSensor <=> cs.colourSensor;
		sortingSystem.beltSensorWhite <=> beltSensorWhite.sensor;
		sortingSystem.beltSensorBlack <=> beltSensorBlack.sensor;
		sortingSystem.whiteActuator <=> whiteActuator.actuator;
		sortingSystem.blackActuator <=> blackActuator.actuator;
		sortingSystem.timer <=> timer.timer;
		
	}
}

interface IMaster
{
	in void start();
	in void stop();
	in void emergency();
	in void forceWait();
	in void continue();

	behaviour
	{
		on start: {}
		on stop: {}
		on emergency: {}
		on forceWait: {}
		on continue: {}
	}
}

component Master
{
	provides IMaster master;
	requires IIngest ingest;
	requires ISensor factoryFloorSensor;
	requires ISortingSystem sortingSystem;

	behaviour
	{
		enum State {Off, Idle, Waiting, Error, IngestingDisk, Sorting};
		State state = State.Off;
		bool waitNext = false;
		

		[state.Off]
		{
			on master.start(): {state = State.Idle;}
			on master.stop(): {}
			on master.emergency(): {}
			on master.forceWait(): {}
			on master.continue(): {}
		}
		[state.Idle]
		{
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;}
			on master.forceWait(): {state = State.Waiting;} 
			on master.continue(): {}
			
			on factoryFloorSensor.high(): {
				state = State.IngestingDisk;
				ingest.startIngest();
			}
		}
		[state.Waiting] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;}
			on master.forceWait(): {} 
			on master.continue(): {state = State.Idle;}
		}
		[state.Error] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;}
			on master.forceWait(): {}
			on master.continue(): {} 
		}
		[state.IngestingDisk] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;} 
			on master.forceWait(): {
				waitNext = true;
			}
			on master.continue(): {}
			
			on ingest.finished(): {
				state = State.Sorting;
				sortingSystem.startSorting();
			}
		}
		[state.Sorting] {
			on master.start(): {}
			on master.stop(): {state = State.Off;}
			on master.emergency(): {state = State.Error;} 
			on master.forceWait(): {
			waitNext = true;
			}
			on master.continue(): {}
			
			on sortingSystem.finished(): {
				if (waitNext) {
					waitNext = false;
					state = State.Waiting;
				} else {
					state = State.Idle;
				}
			}
		}
	}
}
